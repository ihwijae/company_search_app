Sub SendKakaoMsgs()

Dim Target As String: Dim sMsg As String
Dim SendAsImage As Long: Dim sImgTxt As String
Dim r As Long: Dim eRow As Long: Dim iRow As Long
Dim blnSend As Boolean
Dim OpenInAdvance As Boolean
Dim ChatRoomType As Integer

Dim vbYN As VbMsgBoxResult

vbYN = MsgBox("문자 발송을 시작하시겠습니까?", vbYesNo)
If vbYN = vbNo Then Exit Sub

With shtSetting
    iRow = 8
    If .Range("C4").Value <> "" Then OpenInAdvance = True
    If .Range("C2").Value = "문자" Then
        SendAsImage = 0
    ElseIf .Range("C2").Value = "그림" Then
        SendAsImage = 1
    Else
        SendAsImage = 2
    End If
    
    eRow = .Cells(.Rows.Count, 3).End(xlUp).Row
    .Range(.Cells(8, 6), .Cells(eRow, 6)).Value = ""
    .Range("D4").Value = "준비중.."
End With

' 새로운 채팅 실행 준비
Application.Wait DateAdd("s", 1, Now)

If SendAsImage <> 0 Then
    Dim shp As Shape
    Dim tshp As Shape
    On Error Resume Next
    Set shp = shtSetting.Shapes("이미지")
    If Err.Number <> 0 Then MsgBox "카카오톡으로 발송할 그림파일을 찾을 수 없습니다." & vbNewLine & "'이미지'라는 이름의 그림 파일을 생성한 후 다시 시도하세요.", vbCritical: shtSetting.Range("D4").Value = "": Exit Sub
    Err.Clear
    Set shp = shtSetting.Shapes("텍스트")
    If Err.Number <> 0 Then MsgBox "'텍스트'라는 이름의 텍스트 상자를 찾을 수 없습니다." & vbNewLine & "'텍스트'라는 이름의 텍스트 상자를 시트에 추가한 후 다시 시도하세요.", vbCritical: shtSetting.Range("D4").Value = "": Exit Sub
    Err.Clear
    On Error GoTo 0
    Dim ImgValid As Variant
    ImgValid = ImgValidation
    If ImgValid(0) > 1 Then MsgBox "현재 통합문서에 '텍스트'라는 이름의 도형이 2개 이상 존재합니다." & vbNewLine & "'텍스트' 도형을 하나만 남긴 후, 다시 실행하세요.", vbCritical: shtSetting.Range("D4").Value = "": Exit Sub
    If ImgValid(1) > 1 Then MsgBox "현재 통합문서에 '이미지'라는 이름의 도형이 2개 이상 존재합니다." & vbNewLine & "'이미지' 도형을 하나만 남긴 후, 다시 실행하세요.", vbCritical: shtSetting.Range("D4").Value = "": Exit Sub
End If

' 채팅 우선열기 TRUE 일 경우 채팅창 우선 실행
If OpenInAdvance = True Then
    For r = iRow To eRow
        Target = shtSetting.Cells(r, 3).Value
        ChatRoomType = GetChatType(r)
        mod_fx.ActiveChat Target, ChatRoomType
        SetDelay 20
    Next
End If

' 문자 발송
For r = iRow To eRow
    Application.ScreenUpdating = False
    ChatRoomType = GetChatType(r)
    Target = shtSetting.Cells(r, 3).Value
    sMsg = shtSetting.Cells(r, 4).Value
    sImgTxt = shtSetting.Cells(r, 5).Value
    SetDelay 20
    blnSend = mod_fx.SendKakao(Target, sMsg, sImgTxt, SendAsImage, iDelay, OpenInAdvance, ChatRoomType)
    If blnSend = True Then shtSetting.Cells(r, 6).Value = 1 Else shtSetting.Cells(r, 6).Value = 0
    shtSetting.Range("D4").Value = Format((r - 7) / (eRow - 7), "0%") & " 진행 중..."
    Application.ScreenUpdating = True
    DoEvents
Next

' 안내메세지 출력
AppActivate Application.Caption
MsgBox "문자 발송을 완료하였습니다.", vbInformation, "오빠두엑셀"
shtSetting.Range("D4").Value = "-"

End Sub

Function GetChatType(r) As Integer
    If shtSetting.Cells(r, 2).Value = "친구" Then
        GetChatType = 0
    ElseIf shtSetting.Cells(r, 2).Value = "오픈채팅" Then
        GetChatType = 2
    Else
        GetChatType = 1
    End If
End Function

Function ImgValidation()

Dim ws As Worksheet
Dim TxtCount As Long: Dim ImgCount As Long

For Each ws In ThisWorkbook.Worksheets
    For Each shp In ws.Shapes
        If shp.Name = "텍스트" Then
            TxtCount = TxtCount + 1
        End If
        If shp.Name = "이미지" Then
            ImgCount = ImgCount + 1
        End If
    Next shp
Next ws

ImgValidation = Array(TxtCount, ImgCount)
    
End Function

Sub 초기화()

shtSetting.Range("B8:F37").ClearContents

End Sub
Sub 실패내역남기기()

    Dim vbYN As VbMsgBoxResult
    Dim ws As Worksheet
    Dim rng As Range
    Dim arr As Variant
    Dim resultArr() As Variant
    Dim i As Long, j As Long
    Dim rowCount As Long, colCount As Long
    Dim destRow As Long
    
    vbYN = MsgBox("발송 결과 중 실패 내역만 남기시겠습니까?", vbYesNo)
    If vbYN = vbNo Then Exit Sub
    
    ' 현재 시트
    Set ws = ActiveSheet
    
    ' 범위 지정
    Set rng = ws.Range("B8:F37")
    arr = rng.Value
    
    rowCount = UBound(arr, 1)
    colCount = UBound(arr, 2)
    
    ' 결과 배열 크기 재정의
    ReDim resultArr(1 To rowCount, 1 To colCount)
    
    ' 채워 넣을 행 위치
    destRow = 1
    
    ' 조건 만족하는 행만 배열에 저장
    For i = 1 To rowCount
        If arr(i, colCount) = 0 Then
            For j = 1 To colCount
                resultArr(destRow, j) = arr(i, j)
            Next j
            destRow = destRow + 1
        End If
    Next i
    
    ' 나머지는 빈칸 처리
    For i = destRow To rowCount
        For j = 1 To colCount
            resultArr(i, j) = ""
        Next j
    Next i
    
    ' 결과 다시 시트에 반영
    rng.Value = resultArr
End Sub
